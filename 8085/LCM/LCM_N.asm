;LCM OF N NUMBERS
;INPUT			: 00A0H <- NUMBER OF DATA
;			  00A1H <- LIST BASE ADDRESS
;OUTPUT			: 00B0H <- LCM
		LXI H, 00A0H
		DCR M
		INX H
		MOV D, M
LOOP:		INX H
		MOV B, D
		MOV C, M
		CALL MUL
		MOV E, B
		MOV B, D
		MOV C, M
		CALL GCD
		MOV C, E
		CALL DIV
		MOV D, C
		LDA 00A0H
		DCR A
		STA 00A0H
		JNZ LOOP
		MOV A, D
		STA 00B0H
		HLT
		
;SUBROUTINE FOR MULTIPLICATION OF 2 NUMBERS
;INPUT			: B <- MULTIPLIER
;			  C <- MULTIPLICAND
;OUTPUT			: B <- PRODUCT
;REGISTERS AFFECTED	: A, B, C
MUL:		SUB A
MUL_LOOP:	ADD B
		DCR C
		JNZ MUL_LOOP
		MOV B, A
		RET

;SUBROUTINE FOR GCD OF 2 NUMBERS
;INPUT			: B, C <- NUMBERS
;OUTPUT			: B <- GCD
;REGISTERS AFFECTED	: A, B, C
GCD:		MOV A, B
		CMP C 
		JC GCD_ELSE
		SUB C
		MOV B, A
		JMP GCD_ENDIF
GCD_ELSE:	MOV A, C
		SUB B
		MOV C, A
GCD_ENDIF:	MOV A, B
		CMP C
		JNZ GCD
		MOV B,A
		RET

;SUBROUTINE FOR DIVISION OF 2 NUMBERS
;INPUT  		: B <- DIVIDER
;			  C <- DIVIDEND
;OUTPUT			: B <- REMAINDER
;			  C <- QUOTIENT
;REGISTERS AFFECTED	: A, B, C
DIV:		MOV A,C
		MVI C,00H
DIV_LOOP:	CMP B
		JC DIV_END
		SUB B
		INR C
		JMP DIV_LOOP
DIV_END:	MOV B,A
		RET

